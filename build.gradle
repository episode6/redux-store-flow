buildscript {
  ext {
    kotlin_version = libs.versions.kotlin.core.get()
    dokka_version = libs.versions.dokka.core.get()
  }
}
plugins {
  id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version" apply false
  id 'org.jetbrains.dokka' version "$dokka_version" apply true
}

allprojects {
  group = "com.episode6.redux"
  version = "1.0.0-SNAPSHOT"
}

tasks.wrapper {
  gradleVersion = libs.versions.gradle.core.get()
  distributionType = Wrapper.DistributionType.ALL
}

def dokkaDir = "docs/dokka"
task clearDocsDir(type: Delete) {
  delete(dokkaDir)
  doLast { file("$rootDir/$dokkaDir").mkdirs() }
}

tasks.dokkaHtmlMultiModule {
  dependsOn("clearDocsDir")
  outputDirectory.set(file("$rootDir/$dokkaDir"))
}

task syncDocs {
  dependsOn("dokkaHtmlMultiModule")
}
